// faker

plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'eclipse'
    id 'idea'
    
    // keep dependencies up-to-date!
    id 'com.github.ben-manes.versions' version '0.27.0'

    // useful for creating immutable java beans
    id 'org.inferred.processors' version '2.2.0'

    // to ensure clean code
    id "net.ltgt.errorprone" version "0.7.1"

    // for deployment to Maven Central
    id "io.codearte.nexus-staging" version "0.21.1"

    // for building fat jar
    id "com.github.johnrengelman.shadow" version "5.1.0"

    // useful utilities
    id "com.arakelian.multitool" version "2.4.0"
}

group   = 'com.arakelian'

wrapper {
    gradleVersion = '5.6.3'
}

multitool {
    relocates += [
        'org.apache.commons'          : 'repackaged.com.arakelian.faker.${prefix}org.apache.commons',
        'com.google.common'           : 'repackaged.com.arakelian.faker.${prefix}com.google.common',
        'com.google.thirdparty'       : 'repackaged.com.arakelian.faker.${prefix}com.google.thirdparty',
        'javax.annotation'            : 'repackaged.com.arakelian.faker.${prefix}javax.annotation',
    ]
    
    proguardConfiguration {
        keep includedescriptorclasses:true, 'class com.arakelian.** { *; }'
    }
}

dependencies {
    processor 'org.immutables:value:2.7.5'
    
    // annotations
    compile 'org.immutables:value-annotations:2.7.5'

    // configure errorprone version
    errorprone 'com.google.errorprone:error_prone_core:2.3.3'

    compile 'com.arakelian:more-commons:2.3.0'
    compile 'com.arakelian:key-value-store:2.4.0'
    compile 'com.arakelian:jackson-utils:2.4.0'

    // shadow Guava due to frequent downstream incompatibility issues
    shadow  'com.google.guava:guava:28.1-jre'

    // shadow stuff we use very little of, to reduce dependencies
    shadow  'org.apache.commons:commons-lang3:3.9'

    // logging
    testCompile 'org.apache.logging.log4j:log4j-api:2.12.1'
    testCompile 'org.apache.logging.log4j:log4j-core:2.12.1'
    testCompile 'org.apache.logging.log4j:log4j-slf4j-impl:2.12.1'
    testCompile 'org.slf4j:jcl-over-slf4j:1.7.29'
    testCompile 'org.slf4j:jul-to-slf4j:1.7.29'
    compile     'org.slf4j:slf4j-api:1.7.29'

    // for unit testing
    testCompile 'junit:junit:4.12'
    testCompile 'net.javacrumbs.json-unit:json-unit:2.11.1'
    testCompile 'com.arakelian:more-commons:2.3.0:tests'
    testCompile 'com.arakelian:jackson-utils:2.3.0:tests'
}
