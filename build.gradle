// faker

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.guardsquare:proguard-gradle:7.2.0'  // The ProGuard Gradle plugin.
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'eclipse'
    id 'idea'
    
    // keep dependencies up-to-date!
    id 'com.github.ben-manes.versions' version '0.41.0'

    // useful for creating immutable java beans
    id 'org.inferred.processors' version '3.6.0'

    // to ensure clean code
    id "net.ltgt.errorprone" version "2.0.2"

    // for deployment to Maven Central
    id "io.codearte.nexus-staging" version "0.30.0"

    // for building fat jar
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group   = 'com.arakelian'
version = '3.9.1'


apply from: "core.gradle"

wrapper {
    gradleVersion = '7.3.3'
}

publishing.publications.mavenJava {
    pom {
        name = "Faker"
        description = "Faker is library for generating high-quality fake data, such as names, addresses and phone numbers."
        url = "https://github.com/arakelian/faker"
        
        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        
        developers {
            developer {
                id = 'arakelian'
                name = 'Greg Arakelian'
                email = 'greg@arakelian.com'
            }
        }
        
        scm {
            connection = 'scm:git:https://github.com/arakelian/faker.git'
            developerConnection = 'scm:git:git@github.com:arakelian/faker.git'
            url = 'https://github.com/arakelian/faker.git'
        }
    }
	pom.withXml {
		Node pomNode = asNode()
		pomNode.dependencies.'*'.findAll() {
		    it.artifactId.text() in [ 'value-annotations', 'guava', 'commons-io', 'commons-lang3' ]
		}.each() {
		    it.parent().remove(it)
		}
	}
}

dependencies {
    processor 'org.immutables:value:2.9.0'
    
    // annotations
    api 'org.immutables:value-annotations:2.9.0'
    
    // configure errorprone version
    errorprone 'com.google.errorprone:error_prone_core:2.11.0'

    api 'com.arakelian:more-commons:3.3.0'
    api 'com.arakelian:jackson-utils:3.7.1'

    // shadow Guava due to frequent downstream incompatibility issues
    shadow      'com.google.guava:guava:31.0.1-jre'

    // shadow stuff we use very little of, to reduce dependencies
    shadow      'org.apache.commons:commons-lang3:3.12.0'

    // logging
    testImplementation 'org.apache.logging.log4j:log4j-api:2.17.1'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.17.1'
    testImplementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.17.1'
    testImplementation 'org.slf4j:jcl-over-slf4j:1.7.32'
    testImplementation 'org.slf4j:jul-to-slf4j:1.7.32'
    api                'org.slf4j:slf4j-api:1.7.32'

    // for unit testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'net.javacrumbs.json-unit:json-unit:2.28.0'
    testImplementation 'com.arakelian:more-commons:3.3.0:tests'
    testImplementation 'com.arakelian:jackson-utils:3.7.1:tests'
}

shadowJar {
    // need to update any META-INF/services to relocated class names
    mergeServiceFiles()

    // we don't want poms for third-party stuff
    exclude 'META-INF/maven/**/*'
    exclude 'META-INF/**/*.SF'
    exclude 'META-INF/**/*.DSA'
    exclude 'META-INF/**/*.RSA'

    // we are only shadowing 'shadow' dependencies
    configurations = [
        project.configurations.shadow
    ]

    relocate 'com.google.common',       'repackaged.com.arakelian.faker.com.google.common'
    relocate 'com.google.thirdparty',   'repackaged.com.arakelian.faker.com.google.thirdparty'
    relocate 'javax.annotation',        'repackaged.com.arakelian.faker.javax.annotation'
    relocate 'org.apache.commons',      'repackaged.com.arakelian.faker.org.apache.commons'
}

minify {
    // classes in this jar are minified and copied to output jar
    injars shadowJar.archivePath
    
    // output jar
    outjars project.ext.minifyJarFile
    
    // jars required on classpath at runtime, including Java runtime
    libraryjars project.ext.libraryJars()

    // classes that we need to keep
    keep includedescriptorclasses:true, 'class com.arakelian.** { *; }'
}
